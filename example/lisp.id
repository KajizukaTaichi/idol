import stdlib.id;

func ( lisp program ) {
    if ({ type of program } == "list") {
        let evaled = [];
        for atom in program {
            let evaled = evaled + { lisp atom };
        };
        let program = evaled;

        if (program :: 0 == "+") {
            let result = program :: 1;
            for i in { slice program { range 2 { len program } 1 } } {
                let result = result + i;
            };
            (result)
        } else {
            if (program :: 0 == "-") {
                let result = program :: 1;
                for i in { slice program { range 2 { len program } 1 } } {
                    let result = result - i;
                };
                (result)
            } else {
                if (program :: 0 == "*") {
                    let result = program :: 1;
                    for i in { slice program { range 2 { len program } 1 } } {
                        let result = result * i ;
                    };
                    (result)
                } else {
                    if (program :: 0 == "/") {
                        let result = program :: 1;
                        for i in { slice program { range 2 { len program } 1 } } {
                            let result = result / i ;
                        };
                        (result)
                    } else {
                        fault
                    }
                }
            }
        }
    } else program
};

func ( parse source ) {
    if ((source :: 0 == "(") & (source :: ({ len source } - 1) == ")")) {
        let tokens = { tokenize { concat-text { slice source { range 1 ({ len source } - 1) 1 } } } };
        let result = [];
        for i in tokens {
            let result = result + { parse i };
        };
        (result)
    } else {
        if { cast source to number } it else source
    }
};

func ( tokenize source ) {
    let tokens = [];
    let current = "";
    let nest = 0;

    for c in source {
        if (c == "(") {
            let nest = nest + 1;
            let current = current + c;
        } else {
            if (c == ")") {
                let nest = nest - 1;
                let current = current + c;
            } else {
                if ((c == " ") & (nest == 0)) {
                    if (current != "") {
                        let tokens = tokens + current;
                        let current = "";
                    }
                } else {
                    let current = current + c;
                }
            }
        }
    };

    if ((nest == 0) & (current != "")) {
        let tokens = tokens + current;
    };

    (tokens)
};

puts "idol Lisp";
while true {
    let s-expr = { input "> " };
    if (s-expr == ":q") {
        exit
    };
    if { lisp { parse s-expr } } {
        puts it
    } else {
        puts "Error! something is wrong"
    };
}
